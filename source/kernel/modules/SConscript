#
# Copyright (C) 2014 Gil Mendes
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

# Module sub dirs
subdirs = ['platform']

#############################
# Module environment set-up #
#############################

Import('config', 'manager')

# Create the module environment.
env = manager.Clone(manager['kernel'], name = 'module', flags = {
		# Modules default to not exporting symbols.
		'CCFLAGS': ['-fno-common', '-fvisibility=hidden'],
})

dist = manager['dist']
dist['MODULES'] = []

# The emitter adds all modules to the distribution environment's module list.
def module_emitter(target, source, env):
		manager['dist']['MODULES'].append(target[0])
		return target, source
env['BUILDERS']['Module'] = Builder(
		action = [
				Action('$LD -r -o $TARGET $SOURCES', '$LINKCOMSTR'),
				Action('$STRIP --strip-debug $TARGET', None),
		],
		suffix = '.km',
		src_suffix = '$OBJSUFFIX',
		src_builder = 'Object',
		emitter = module_emitter
)

# Get all of the modules and include directories into the array
SConscript(dirs=subdirs, exports=['env'])
